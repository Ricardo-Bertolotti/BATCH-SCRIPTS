@ECHO OFF

CHCP 1252>NUL

SET LINHAS=------------------------------------------------------------------

ECHO.
NET SESSION >nul 2>&1 && GOTO MAIN

COLOR 04
ECHO %LINHAS%
ECHO.
ECHO ESTE SCRIPT REQUER PRIVILÉGIOS DE ADMINISTRADOR
ECHO POR FAVOR, EXECUTE O SCRIPT COMO ADMINISTRADOR
ECHO.
ECHO %LINHAS%
ECHO.
PAUSE
EXIT /B

:MAIN
COLOR 01
ECHO %LINHAS%
ECHO.
ECHO. [ 1 ] - DESATIVAR POLÍTICAS DE SEGURANÇA
ECHO.
ECHO. [ 2 ] - ATIVAR POLÍTICAS DE SEGURANÇA
ECHO.
ECHO %LINHAS%
ECHO.
CHOICE /C 12 /N /M "ESCOLHA UMA OPÇÃO : "
ECHO.
CLS

IF %ERRORLEVEL%==1 (

SET CONTROLE=DESATIVADA
SET REG_1=1
FOR %%A IN (REG_2 REG_3 REG_4) DO SET %%A=0

) ELSE (

SET CONTROLE=ATIVADA
SET REG_4=0
FOR %%A IN (REG_1 REG_2 REG_3) DO SET %%A=1
)

REG ADD "HKLM\SOFTWARE\Microsoft\Windows\CurrentVersion\Policies\System" /v DisableCAD /t REG_DWORD /d %REG_1% /f >nul
REG ADD "HKLM\SOFTWARE\Microsoft\Windows\CurrentVersion\Policies\System" /v ConsentPromptBehaviorAdmin /t REG_DWORD /d %REG_2% /f >nul
REG ADD "HKLM\SOFTWARE\Microsoft\Windows\CurrentVersion\Policies\System" /v ConsentPromptBehaviorUser /t REG_DWORD /d %REG_3% /f >nul
REG ADD "HKLM\SOFTWARE\Microsoft\Windows\CurrentVersion\Policies\System" /v EnableLUA /t REG_DWORD /d %REG_4% /f >nul

COLOR 02
ECHO.
ECHO %LINHAS%
ECHO.
ECHO SCRIPT EXECUTADO COM SUCESSO!!!
ECHO.
ECHO FUNÇÃO "CTRL + ALT + DEL" %CONTROLE% PARA LOGIN
ECHO CONFIRMAÇÃO PARA REQUISIÇÕES EM MODO ADMIN %CONTROLE%
ECHO.
ECHO REINICIE SUA MÁQUINA PARA QUE AS ALTERAÇÕES SEJAM APLICADAS!!
ECHO.
ECHO %LINHAS%
ECHO.
PAUSE

EXIT